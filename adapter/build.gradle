plugins {
	id 'org.jetbrains.kotlin.jvm'
	id 'application'
	id 'maven'
}

version = projectVersion

mainClassName = 'fwcd.ktda.KDAMainKt'
description = 'Debug Adapter for Kotlin'

sourceCompatibility = 1.8
targetCompatibility = 1.8

startScripts {
	applicationName = "kotlin-debug-adapter"
}

repositories {
	// TODO: Installing dependencies from this (public) GPR Maven repo still requires authentication
	maven { url = uri('https://maven.pkg.github.com/fwcd/kotlin-language-server') }
}

dependencies {
	// The JSON-RPC and Debug Adapter Protocol implementations
	implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j.debug:0.6.0'
	implementation 'org.jetbrains.kotlin:kotlin-stdlib'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation 'kotlin-language-server:shared:0.5.2'

	// The Java Debug Interface classes (com.sun.jdi.*)
	implementation files("${System.properties['java.home']}/../lib/tools.jar")
	testImplementation 'junit:junit:4.12'
}

task fixFilePermissions(type: Exec) {
	// When running on macOS or Linux the start script
	// needs executable permissions to run.
	onlyIf { !System.getProperty('os.name').toLowerCase().contains('windows') }
	commandLine 'chmod', '+x', "${installDist.destinationDir}/bin/kotlin-debug-adapter"
}

test {
	testLogging {
		events 'failed'
		exceptionFormat 'short'
	}
}

run {
	standardInput = System.in
}

distZip {
	archiveName = "${project.name}.zip"
}

installDist.finalizedBy fixFilePermissions
