plugins {
	id 'com.github.node-gradle.node' version '2.0.0'
}

def resourcesDir = file("resources")

task copyIcon(type: Copy) {
	from "$rootDir/Icon128.png"
	into resourcesDir
}

/** Prepares the debug adapter binaries for packaging. */
task copyDebugAdapter(type: Sync) {
	dependsOn ':adapter:installDist'
	from project(':adapter').tasks.installDist.destinationDir
	into "$resourcesDir/install"
}

/**
 * Prepares resources used by the extension.
 * Can be useful if the extension's code is
 * built separately, e.g. using 'npm run watch'.
 */
task prepare { dependsOn copyIcon, copyDebugAdapter }

npm_run_compile { dependsOn prepare }

/** Compiles the VSCode extension's TypeScript code. */
task compile { dependsOn npm_run_compile }

npm_run_packageextension { dependsOn compile }

/** Packages the VSCode extension. */
task packageExtension { dependsOn npm_run_packageextension }

/** Packages and tests the VSCode extension. */
task build { dependsOn packageExtension }
